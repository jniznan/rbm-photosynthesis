begin model

begin parameters
	deex_flu     670e5  # de-excitation (fluorescence) 67000000
	deex_heat      2e5  # de-excitation (heat) 200000
	deex         672e5
	ch_re        50e10  # Charge recombination 500000000000
	ch_se        50e10  # Primary charge separation  500000000000
	stab_gen     25e10  # ChlD1+ reduction by P680 250000000000
	stab_re     6.7e10  # Reverse electron flow from ChlD1 to P680+  67000000000
	qa_red     0.48e10  # Qa reduction by PhD1-  4800000000
	qa_rev     0.24e10  # Reverse electron flow from Qa- to PhD1  2400000000
	extrans    1.92e10  # Energy transfer between A and ChlD1  k1  19200000000
	extrans2    2.5e10  # Energy transfer between A and ChlD1  k2  25000000000
end parameters

begin molecule types
	PSII(P680~n~p,Qa~n~m,PhD1~n~m,ac~n~exc,ChlD1~n~p~exc)
end molecule types

begin seed species
	PSII(P680~n,Qa~n,PhD1~n,ac~exc,ChlD1~n)           1
end seed species

begin reaction rules
	# Deexcitation - antenna:
	PSII(P680~n,Qa~n,PhD1~n,ac~exc,ChlD1~n) -> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~n)   deex_flu
	PSII(P680~n,Qa~n,PhD1~n,ac~exc,ChlD1~n) -> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~n)   deex_heat
	# PSII(ac~exc) -> PSII(ac~n)   deex

	# Deexcitation - ChlD1:
	PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc) -> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~n)   deex_flu
	PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc) -> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~n)   deex_heat
	# PSII(ChlD1~exc) -> PSII(ChlD1~n)   deex
	
	# Excitation Transfer:
	PSII(P680~n,Qa~n,PhD1~n,ac~exc,ChlD1~n) <-> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc)   extrans, extrans2
	#PSII(ac~exc,ChlD1~n) <-> P(ac~n,ChlD1~exc)  extrans, extrans2
	
	# Primary charge separation / recombination:
	PSII(P680~n,Qa~n,PhD1~m,ac~n,ChlD1~p) -> PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc)    ch_re
	PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc) -> PSII(P680~n,Qa~n,PhD1~m,ac~n,ChlD1~p)    ch_se
	#PSII(PhD1~n,ChlD1~exc) <-> PSII(PhD1~m,ChlD1~p)    ch_se, ch_re
	
	# Stable pair generation:
	PSII(P680~n,Qa~n,PhD1~m,ac~n,ChlD1~p) -> PSII(P680~p,Qa~n,PhD1~m,ac~n,ChlD1~n)   stab_gen
	PSII(P680~p,Qa~n,PhD1~m,ac~n,ChlD1~n) -> PSII(P680~n,Qa~n,PhD1~m,ac~n,ChlD1~p)   stab_re
	
	# Quinone Qa reduction:
	PSII(P680~p,Qa~n,PhD1~m,ac~n,ChlD1~n) -> PSII(P680~p,Qa~m,PhD1~n,ac~n,ChlD1~n)    qa_red
	PSII(P680~p,Qa~m,PhD1~n,ac~n,ChlD1~n) -> PSII(P680~p,Qa~n,PhD1~m,ac~n,ChlD1~n)    qa_rev
end reaction rules

begin observables
	Molecules    Ant_exc              PSII(P680~n,Qa~n,PhD1~n,ac~exc,ChlD1~n) 
	Molecules    ChlD_exc            PSII(P680~n,Qa~n,PhD1~n,ac~n,ChlD1~exc) 
end observables

end model

## actions ##
# construct reaction network
generate_network({overwrite=>1})

simulate({method=>ode,t_start=>1e-12,t_end=>1e-6,n_steps=>50000})



