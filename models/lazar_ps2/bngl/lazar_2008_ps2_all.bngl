begin model

begin parameters
    # Rate constants:
	kL     4000   # 1/sec Charge separation rate, Lazar_2003
	kRec  10000   # 1/sec Charge recombination rate, Lazar_2003
	k01   20000   # 1/sec Electron donation from S0 to P680+ rate constant, Lazar_2003
	k12   10000
	k23    3330
	k30    1000
	kAB1   3500   # 1/sec Electron transport from Qa- to Qb, Lazar_2003
	kAB2   1750   # 1/sec Electron transport from Qa- to Qb-, Lazar_2003
	kBA1    175   # 1/sec Electron transport from Qb- to Qa, Lazar_2003
	kBA2     35   # 1/sec Electron transport from Qb2- to Qa, Lazar_2003
	kfExch  250 
	kbExch  250
	kOx      10
	kRed     10
end parameters

begin molecule types
	PSII(P680~n~p,Qa~n~m,S~0~1~2~3,Qb~empty~n~m~2m)
	PQ(Qb~n~2m)
end molecule types

begin seed species
end seed species

begin reaction rules
	# Light induced charge separation between P680 and Qa and charge recombination:
	PSII(P680~n,Qa~n) <-> PSII(P680~p,Qa~m)  kL, kRec
	# Electron donation from S-states of OEC to P680+
	PSII(P680~p,S~0) -> PSII(P680~n,S~1)  k01
	PSII(P680~p,S~1) -> PSII(P680~n,S~2)  k12
	PSII(P680~p,S~2) -> PSII(P680~n,S~3)  k23
	PSII(P680~p,S~3) -> PSII(P680~n,S~0)  k30
	# Electron transport from Qa- to Qb:
	PSII(Qa~m,Qb~n) <-> PSII(Qa~n,Qb~m)   kAB1, kBA1
	PSII(Qa~m,Qb~m) <-> PSII(Qa~n,Qb~2m)  kAB2, kBA2
	# Exchange of doubly reduced Qb with oxidized PQ molecule from the PQ pool.
	# by one second order equation:
	PSII(Qb~2m) + PQ(Qb~n) <-> PSII(Qb~n) + PQ(Qb~2m)  kfExch, kbExch  
	# Reversible reoxidation of reduced PQ molecules from the PQ pool:
	PQ(Qb~2m) <-> PQ(Qb~n)  kOx, kRed	
end reaction rules

begin observables
	# unquenched fluorescence signal:
	Molecules    Qa_m    PSII(Qa~m)
	Molecules    PQ      PQ(Qb~n)
end observables

end model

## actions ##
# construct reaction network
generate_network({overwrite=>1})

simulate({method=>ode,t_start=>1e-6,t_end=>1,n_steps=>5000})



